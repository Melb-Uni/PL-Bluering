(define (domain sokoban_sequential)
  (:requirements :typing :action-costs :strips)
  
  (:predicates (position ?l)
                (clear ?l )
                (at ?t ?l)
                (playert ?p)
                (stonet ?s)
                (at_goal ?s)
               (IS_GOAL ?l)
	       (IS_NONGOAL ?l)
               (MOVE_DIR ?from ?to ?dir ))
  (:functions (total-cost) - number)

  (:action move
   :parameters (?p  ?from ?to  ?dir )
   :precondition (and 
                      (position ?from)
                      (position ?to)
                      (playert ?p)
                      (at ?p ?from)
                      (clear ?to)
                      (MOVE_DIR ?from ?to ?dir)
                      )
   :effect       (and (not (at ?p ?from))
                      (not (clear ?to)) 
                      (at ?p ?to)
                      (clear ?from)
                      )
   )

  (:action push_to_nongoal
   :parameters (?p  ?s 
                ?ppos ?from ?to 
                ?dir )
   :precondition (and (at ?p ?ppos)
                      (position ?from)
                      (position ?to)
                      (position ?ppos)
                      (playert ?p)
                      (stonet ?s)
                      (at ?s ?from)
                      (clear ?to)
                      (MOVE_DIR ?ppos ?from ?dir)
                      (MOVE_DIR ?from ?to ?dir)
                      (IS_NONGOAL ?to)
                      )
   :effect       (and (not (at ?p ?ppos))
                      (not (at ?s ?from))
                      (not (clear ?to))
                      (at ?p ?from)
                      (at ?s ?to)
                      (clear ?ppos)
                      (not (at_goal ?s))
                      (increase (total-cost) 1)
                      )
   )

  (:action push_to_goal
   :parameters (?p  ?s ?ppos ?from ?to ?dir)
   :precondition (and (at ?p ?ppos)
                      (position ?from)
                      (position ?to)
                      (position ?ppos)
                      (playert ?p)
                      (stonet ?s)
                      (at ?s ?from)
                      (clear ?to)
                      (MOVE_DIR ?ppos ?from ?dir)
                      (MOVE_DIR ?from ?to ?dir)
                      (IS_GOAL ?to)
                      )
   :effect       (and (not (at ?p ?ppos))
                      (not (at ?s ?from))
                      (not (clear ?to))
                      (at ?p ?from)
                      (at ?s ?to)
                      (clear ?ppos)
                      (at_goal ?s)
                      (increase (total-cost) 1)
                      )
   )
)
